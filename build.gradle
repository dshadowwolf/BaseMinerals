def corePlugin = ""

buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/groups/public"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
        classpath "com.matthewprenger:CurseGradle:1.0-SNAPSHOT"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.2.1"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "com.matthewprenger.cursegradle"
apply plugin: "maven-publish"
apply plugin: "org.sonarqube"

javadoc {
    failOnError = false
}

def mod_file = getModFile()
def mc_version = "1.12"
def short_version = getVersion("VERSION", mod_file)
version = mc_version + "-" + short_version
if (System.getenv().BUILD_NUMBER) {
    version += "." + System.getenv().BUILD_NUMBER
}
group = "com.mcmoddev"
archivesBaseName = "BaseMinerals"
sourceCompatibility = targetCompatibility = "1.8"

class Secrets {
    def data = null

    def getProperty(String key) {
        return data ? data[key] : ""
    }
}

import groovy.json.JsonSlurper

def secretFile
if (System.getenv().SECRET_FILE) {
    secretFile = file System.getenv().SECRET_FILE
} else {
    secretFile = file "secret.json"
}

project.ext.secret = new Secrets()
if (secretFile.exists()) {
    secretFile.withReader {
        project.ext.secret.data = new JsonSlurper().parse it
    }
}

minecraft {
    version = "1.12-14.21.0.2327"
    runDir = "run"
    mappings = "snapshot_20170615"
//  coreMod = corePlugin
//  clientJvmArgs = ["-Dfml.coreMods.load=$corePlugin"]
//  serverJvmArgs = ["-Dfml.coreMods.load=$corePlugin"]
}

repositories {
    maven {
	    name "SpongePowered"
        url 'https://repo.spongepowered.org/maven/'
    }
    maven { // Mantle, TCon, JEI
        name "DVS1 Maven FS"
        url "http://dvs1.progwml6.com/files/maven"
    }
    maven { // CCL, CCC, NEI
        name "Chickenbones Repo"
        url "http://chickenbones.net/maven/"
    }
    maven { // The One Probe
        name "tterrag"
        url "http://maven.tterrag.com/"
    }
    maven {
        name "MMD"
        url "https://maven.mcmoddev.com/"
    }
    maven { // MCMultipart
        name "amadornes"
        url "http://maven.amadornes.com/"
    }
    maven { // Tesla
        name "epoxide"
        url "http://maven.epoxide.org"
    }
    maven { // CraftTweaker, ZenScript
        name 'jared maven'
        url "http://blamejared.com/maven"
    }
    maven { // Mekanism, TAIGA
        name "jitpack"
        url "https://jitpack.io"
    }
    maven { // IC2
        name "industrialcraft"
        url "http://maven.ic2.player.to"
    }
    maven { // Waila
    	name "tehnut"
    	url "http://tehnut.info/maven"
    }
    maven { // CoFH
    	name "Covers Maven"
    	url "http://maven.covers1624.net"
    }
}

dependencies {
    compile "slimeknights:TConstruct:1.11.2-${tconstruct_version}:deobf"
    compile "slimeknights.mantle:Mantle:1.11.2-${mantle_version}:deobf"
    compile "com.mcmoddev:OreSpawn:1.11.2-${orespawn_version}:dev"
    compile "com.mcmoddev:BaseMetals:1.11.2-${bme_version}:deobf"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from (sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        expand "version": short_version, "mcversion": project.minecraft.version
    }

    from (sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

jar {
    manifest {
//      attributes "FMLCorePluginContainsFMLMod": "true"
//      attributes "FMLCorePlugin": corePlugin
    }
}

/*
task apiJar(type: Jar) {
    from apiClasses
    classifier = 'apiClasses'
}
*/

task deobfJar(type: Jar) {
    classifier = "deobf"
    from sourceSets.main.output
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.getDestinationDir()
}

artifacts {
//  archives apiJar
    archives deobfJar
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
            artifact sourceJar {
                classifier "sources"
            }
/*
            artifact apiJar {
                classifier "api"
            }
*/
            artifact deobfJar {
                classifier "deobf"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
    repositories {
        maven {
            credentials {
                username secret.username
                password secret.password
            }
            url secret.url
        }
    }
}

/*
curseforge {
    apiKey = secret.curseforgeAPIKey
    project {
        id = ""
        changelog = file("CHANGELOG.txt")
        releaseType = "release"
        addGameVersion(project.mc_version)
        def projName = "Base Minerals"
        def displayVersion = getVersion("VERSION", mod_file)
        if (System.getenv().BUILD_NUMBER) {
            displayVersion += "." + System.getenv().BUILD_NUMBER
        }
        mainArtifact(jar) {
            displayName = "$projName $displayVersion"
            relations {
                requiredLibrary "mmd-orespawn"
                requiredLibrary "base-metals"
                optionalLibrary "additional-loot-tables"
            }
        }
//        addArtifact(apiJar) {
//            displayName = "$projName $displayVersion API"
//        }
        addArtifact(sourceJar) {
            displayName = "$projName $displayVersion Sources"
        }
        addArtifact(deobfJar) {
            displayName = "$projName $displayVersion Development"
        }
        addArtifact(javadocJar) {
            displayName = "$projName $displayVersion Javadoc"
        }
    }
}
*/

String getModFile() {
    String path = "";
    FileTree tree = fileTree(dir: "src/main/java")
    tree.include "**/*.java"
    tree.visit { element ->
        if (element.file.isFile()) {
            element.file.eachLine { String s ->
                s = s.trim();
                if (s.startsWith("@Mod(")) {
                    path = "src/main/java/$element.relativePath"
                }
            }
        }
    }
    return path;
}

String getVersion(String type, String mod_file) {
    String major = "0";
    String revision = "0";
    String patch = "0";
    String prefix = "public static final String $type = \"";
    File file = file(mod_file)
    file.eachLine { String s ->
        s = s.trim();
        if (s.startsWith(prefix)) {
            s = s.substring(prefix.length(), s.length() - 2);
            String[] pts = s.split("\\.");

            major = pts[0];
            revision = pts[1];
            patch = pts[2];
        }
    }
    return "$major.$revision.$patch";
}
